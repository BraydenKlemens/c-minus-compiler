/*
  Created by: Boris Skurikhin, Brayden Klemens
  File Name: cminus.cup
*/
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
import java_cup.runtime.*;
import absyn.*; 

parser code {:

   /* Constructs an error messsage string, outputs to stderr */
   public void report_error(String message, Object info) {
   
      StringBuffer m = new StringBuffer("Error");
   
      if (info instanceof java_cup.runtime.Symbol) {
         java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
         if (s.left >= 0) {                
            m.append(" in line "+(s.left+1));   
            if (s.right >= 0) 
               m.append(", column "+(s.right+1));
         }
      }
   
      m.append(" : "+message);
      System.err.println(m);
   }
   
   /* Overwrites report fatal error method */
   public void report_fatal_error(String message, Object info) {
      report_error(message, info);
      System.exit(1);
   }

:};

   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal IF, ELSE, RETURN, INT, VOID, WHILE;
terminal LESSTHANEQ, GREATERTHANEQ, CHECKEQUALS, CHECKNOTEQUALS, SETEQUALS, LESSTHAN, GREATERTHAN;
terminal PLUS, MINUS, TIMES, DIVIDE, LBRACKET, RBRACKET, SEMICOLON, LSQUAREBRACKET, RSQUAREBRACKET, LCURLYBRACKET, RCURLYBRACKET, COMMA;
terminal String ID, NUMBER;
terminal ERROR;
   
non terminal DeclarationList program, declaration_list;
non terminal Declaration declaration, function_declaration;
non terminal VarDeclaration parameter, variable_declaration;
non terminal VarDeclarationList parameter_list, block_declarations;
non terminal CompoundStatement compound_statement;
non terminal ExpList statement_list, expression_list;
non terminal Exp expression, statement, exp_statement, compare_exp, math_exp, other_exp, if_statement, loop_statement, return_statement;
non terminal VariableExp var;
non terminal ArrVariableExp arr_var;

precedence right SETEQUALS;
precedence left COMMA, ELSE, ERROR;
precedence nonassoc CHECKEQUALS, CHECKNOTEQUALS, LESSTHAN, LESSTHANEQ, GREATERTHAN, GREATERTHANEQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;

/* ----------------------------Grammar Section-------------------- */
   
start with program;


program	            ::= declaration_list:dec_list
                        {: RESULT = dec_list; :}
                     | 
                        {:
                           parser.report_error("Empty file", null);
                        :}
                     ;

declaration_list     ::= declaration_list:dec_list declaration:dec
                        {:
                           DeclarationList list = dec_list;
                           while( list.tail != null )
                              list = list.tail;
                           list.tail = new DeclarationList(dec, null);
                           RESULT = dec_list;
                        :}
                     | declaration:dec
                        {:
                           RESULT = new DeclarationList(dec, null);
                        :}
                     ;

declaration          ::= function_declaration:func_d
                        {:
                           RESULT = func_d;
                        :}
                     | variable_declaration:var_d
                        {:
                           RESULT = var_d;
                        :}
                     ;

variable_declaration ::= INT:type ID:id SEMICOLON
                        {:
                           RESULT = new NoValDeclaration(
                              typeleft,
                              typeright, 
                              id,
                              new VariableType(typeleft, typeright, VariableType.INT)
                           );
                        :}
                     | INT:type ID:id LSQUAREBRACKET NUMBER:size RSQUAREBRACKET SEMICOLON
                        {:
                           RESULT = new ArrayDeclaration(
                              typeleft,
                              typeright,
                              new VariableType(typeleft, typeright, VariableType.INT),
                              id,
                              new IntExp(sizeleft, sizeright, size)
                           );
                        :}
                     | INT:type error:id LSQUAREBRACKET NUMBER:size RSQUAREBRACKET SEMICOLON
                        {:
                           parser.report_error("Invalid variable name", null);
                           RESULT = new ArrayDeclaration(
                              idleft,
                              idright,
                              new VariableType(typeleft, typeright, VariableType.INT),
                              "ERROR",
                              new IntExp(sizeleft, sizeright, size)
                           );
                        :}
                     | INT:type ID:id LSQUAREBRACKET error:err RSQUAREBRACKET SEMICOLON
                        {:
                           parser.report_error("Invalid array size", null);
                           RESULT = new ArrayDeclaration(
                              typeleft,
                              typeright,
                              new VariableType(typeleft, typeright, VariableType.INT),
                              id,
                              new IntExp(errleft, errright, "ERROR")
                           );
                        :}
                     | error:type ID:id SEMICOLON
                        {:
                           parser.report_error("Invalid variable type", null);
                           RESULT = new NoValDeclaration(
                              typeleft,
                              typeright, 
                              id,
                              new VariableType(typeleft, typeright, VariableType.ERROR)
                           );
                        :}
                     | INT:type error:id SEMICOLON
                        {:
                           parser.report_error("Invalid variable name", null);
                           RESULT = new NoValDeclaration(
                              idleft,
                              idright, 
                              "ERROR",
                              new VariableType(typeleft, typeright, VariableType.INT)
                           );
                        :}
                     ;

function_declaration ::= INT:type ID:id LBRACKET parameter_list:params RBRACKET compound_statement:statement
                           {:
                              RESULT = new FunctionDeclaration(
                                 idleft,
                                 idright,
                                 new VariableType(typeleft, typeright, VariableType.INT),
                                 id,
                                 params,
                                 statement
                              );
                           :}
                        | VOID:type ID:id LBRACKET parameter_list:params RBRACKET compound_statement:statement
                           {:
                              RESULT = new FunctionDeclaration(
                                 idleft,
                                 idright,
                                 new VariableType(typeleft, typeright, VariableType.VOID),
                                 id,
                                 params,
                                 statement
                              );
                           :}
                        ;

parameter_list       ::= parameter_list:list COMMA parameter:param
                        {:
                           VarDeclarationList param_list = list;
                           while (param_list.tail != null) {
                              param_list = param_list.tail;
                           }
                           param_list.tail = new VarDeclarationList(param, null);
                           RESULT = list;
                        :}
                     | parameter:param
                        {:
                           RESULT = new VarDeclarationList(param, null);
                        :}
                     | VOID 
                        {:
                           RESULT = null;
                        :}
                     | 
                        {:
                           RESULT = null;
                        :}
                     ;

parameter            ::= INT:type ID:id LSQUAREBRACKET RSQUAREBRACKET
                        {:
                           RESULT = new ArrayDeclaration(
                              typeleft,
                              typeright,
                              new VariableType(typeleft, typeright, VariableType.INT),
                              id,
                              null
                           );
                        :}
                     | INT:type ID:id
                        {:
                           RESULT = new NoValDeclaration(
                              typeleft,
                              typeright, 
                              id,
                              new VariableType(typeleft, typeright, VariableType.INT)
                           );
                        :} 
                     | error:type 
                        {:
                           parser.report_error("Invalid function parameter", null);
                           RESULT = new NoValDeclaration(
                              typeleft,
                              typeright, 
                              "ERROR",
                              new VariableType(typeleft, typeright, VariableType.ERROR)
                           );
                        :}
                     ;


compound_statement   ::= LCURLYBRACKET block_declarations:declarations statement_list:statements RCURLYBRACKET
                        {:
                           RESULT = new CompoundStatement(declarationsleft, declarationsright, declarations, statements);
                        :}
                     | LCURLYBRACKET block_declarations:declarations error:err RCURLYBRACKET
                        {:
                           parser.report_error("Invalid compound statement body", null);
                           RESULT = new CompoundStatement(
                              declarationsleft,
                              declarationsright,
                              declarations,
                              new ExpList(new OpExp(errleft, errright, OpExp.ERROR), null));
                        :}
                     ;


block_declarations   ::= block_declarations:dec_list variable_declaration:var_dec
                        {:
                           VarDeclarationList list = dec_list;
                           while( list.tail != null )
                              list = list.tail;
                           list.tail = new VarDeclarationList(var_dec, null);
                           RESULT = dec_list;
                        :}
                     |
                        {:
                           RESULT = new VarDeclarationList(null, null);  
                        :}
                     ;

statement_list       ::= statement_list:statement_list statement:statement
                        {:
                           ExpList list = statement_list;
                           while( list.tail != null )
                              list = list.tail;
                           list.tail = new ExpList(statement, null);
                           RESULT = statement_list;
                        :}
                     | 
                        {:
                           RESULT = new ExpList(null, null);
                        :}
                     ;

statement            ::= exp_statement:exp_s
                        {:
                           RESULT = exp_s;
                        :}
                     | compound_statement:comp_s
                        {:
                           RESULT = comp_s;
                        :}
                     | if_statement:if_s 
                        {:
                           RESULT = if_s;
                        :}
                     | loop_statement:loop_s
                        {:
                           RESULT = loop_s; 
                        :}
                     | return_statement:ret_s 
                        {:
                           RESULT = ret_s;
                        :}
                     ;

exp_statement        ::= expression:exp SEMICOLON
                        {:
                           RESULT = exp;
                        :}
                     | SEMICOLON 
                        {:
                           RESULT = null;
                        :}
                     ;

if_statement         ::= IF:i LBRACKET expression:exp RBRACKET statement:sment
                        {:
                           RESULT = new IfExp(ileft, iright, exp, sment, null);
                        :}
                     | IF:i LBRACKET expression:exp RBRACKET statement:sment1 ELSE statement:sment2
                        {:
                           RESULT = new IfExp(ileft, iright, exp, sment1, sment2);
                        :}
                     | IF:i LBRACKET error:err RBRACKET statement:sment
                        {:
                           parser.report_error("Invalid if condition", null);
                           RESULT = new IfExp(ileft, iright, new OpExp(errleft, errright, OpExp.ERROR), sment, null);
                        :}
                     | IF:i LBRACKET error:err RBRACKET statement:sment1 ELSE statement:sment2
                        {:
                           parser.report_error("Invalid if-else condition", null);
                           RESULT = new IfExp(ileft, iright, new OpExp(errleft, errright, OpExp.ERROR), sment1, sment2);
                        :}
                     ;

loop_statement       ::= WHILE:loop LBRACKET expression:exp RBRACKET statement:sment 
                        {:
                           RESULT = new RepeatExp(loopleft, loopright, exp, sment);
                        :}
                     | WHILE:loop LBRACKET error:exp RBRACKET statement:sment
                        {:
                           parser.report_error("Invalid loop condition", null);
                           RESULT = new RepeatExp(loopleft, loopright, new OpExp(expleft, expright, OpExp.ERROR), sment);
                        :}
                     ;

return_statement     ::= RETURN:ret expression:exp SEMICOLON
                        {:
                           RESULT = new ReturnExp(retleft, retright, exp);
                        :}
                     | RETURN:ret SEMICOLON
                        {:
                           RESULT = new ReturnExp(retleft, retright, null);
                        :}
                     | RETURN:ret error:err SEMICOLON
                        {:
                           parser.report_error("Invalid return expression", null);
                           RESULT = new ReturnExp(retleft, retright, new OpExp(errleft, errright, OpExp.ERROR));
                        :}
                     ;

/* X = 5; X[3] = 3; */
/* X < 3 */
/* 7 + 4 + X, (X) */
expression           ::= var:v SETEQUALS expression:exp
                        {:
                           RESULT = new AssignExp(vleft, vright, v, exp);
                        :}
                     | arr_var:v SETEQUALS expression:exp
                        {:
                           RESULT = new AssignExp(vleft, vright, v, exp);
                        :}
                     | compare_exp:exp
                        {:
                           RESULT = exp;
                        :}
                     | math_exp:exp
                        {:
                           RESULT = exp;
                        :}
                     | other_exp:exp 
                        {:
                           RESULT = exp;
                        :}
                     ;

arr_var              ::= ID:id LSQUAREBRACKET expression:exp RSQUAREBRACKET
                        {:
                           RESULT = new ArrVariableExp(idleft, idright, id, exp);
                        :}
                     | ID:id LSQUAREBRACKET error:err RSQUAREBRACKET
                        {:
                           parser.report_error("Invalid array index expression", null);
                           RESULT = new ArrVariableExp(idleft, idright, id, new OpExp(errleft, errright, OpExp.ERROR));
                        :}
                     ;


var                  ::= ID:id
                        {:
                           RESULT = new VariableExp(idleft, idright, id, null);
                        :}
                     ; /* deleted empty thing */

compare_exp          ::= expression:exp1 LESSTHANEQ expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.LESSTHANEQ, exp2);
                        :}
                     | expression:exp1 GREATERTHANEQ expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.GREATERTHANEQ, exp2);
                        :}
                     | expression:exp1 LESSTHAN expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.LESSTHAN, exp2);
                        :}
                     | expression:exp1 GREATERTHAN expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.GREATERTHAN, exp2);
                        :}
                     | expression:exp1 CHECKNOTEQUALS expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.CHECKNOTEQUALS, exp2);
                        :}
                     | expression:exp1 CHECKEQUALS expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.CHECKEQUALS, exp2);
                        :}
                     ;

math_exp             ::= expression:exp1 PLUS expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.PLUS, exp2);
                        :}
                     | expression:exp1 MINUS expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.MINUS, exp2);
                        :}
                     | expression:exp1 TIMES expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.TIMES, exp2);
                        :}
                     | expression:exp1 DIVIDE expression:exp2
                        {:
                           RESULT = new OpExp(exp1left, exp2right, exp1, OpExp.DIVIDE, exp2);
                        :}
                     | LBRACKET expression:exp RBRACKET
                        {:
                           RESULT = exp;
                        :}
                     ;

other_exp            ::= ID:id
                        {:
                           RESULT = new VariableExp(idleft, idright, id, null);
                        :}
                     | ID:id LSQUAREBRACKET expression:exp RSQUAREBRACKET
                        {:
                           RESULT = new ArrVariableExp(idleft, idright, id, exp);
                        :}
                     | ID:id LBRACKET expression_list:list RBRACKET
                        {:
                           RESULT = new CallExpression(idleft, idright, id, list);
                        :}
                     | ID:id LBRACKET RBRACKET
                        {:
                           RESULT = new CallExpression(idleft, idright, id, null);
                        :}
                     | NUMBER:num 
                        {:
                           RESULT = new IntExp(numleft, numright, num);
                        :}
                     | ID:id LBRACKET error:err RBRACKET
                        {:
                           parser.report_error("Invalid function call parameters", null);
                           RESULT = new CallExpression(idleft, idright, id, new ExpList(
                              new OpExp(errleft, errright, OpExp.ERROR), null));
                        :};

expression_list      ::= expression_list:exp_list COMMA expression:exp
                        {:
                           ExpList expList = exp_list;
                           while (expList.tail != null)
                              expList = expList.tail;
                           expList.tail = new ExpList(exp, null);
                           RESULT = exp_list;
                        :}
                     | expression:exp
                        {:
                           RESULT = new ExpList(exp, null);  
                        :}
                     ;
